//DOLF SWORD
//half sword in doom?
version "4.10"

class halfsworder:doomplayer{
	default{
		player.startitem "doso_longsword",1;
		player.displayname "Swordsman";
		player.forwardmove 0.6,0.3;
		player.sidemove 0.7,0.35;
		
		radius 17;
	}
}
class dosomonk:halfsworder{
	default{
		player.startitem "doso_staff",1;
		player.displayname "Monk";
	}
}
class dosoguard:halfsworder{
	default{
		player.startitem "doso_halberd",1;
		player.displayname "Guard";
	}
}
class dosoduelist:halfsworder{
	default{
		player.startitem "doso_rapier",1;
		player.startitem "doso_dagger",1;
		player.displayname "Fencer";
	}
}
class dosoldier:halfsworder{
	default{
		player.startitem "doso_spear",1;
		player.displayname "Soldier";
	}
}

#include "zscript-doso/miscmath.zsc"

#include "zscript-doso/weapon.zsc"

#include "zscript-doso/weapons/swords.zsc"
#include "zscript-doso/weapons/bladedpole.zsc"
#include "zscript-doso/weapons/bluntpole.zsc"

#include "zscript-doso/spawners.zsc"

//handler for mouse input
class doso_inputhandler:eventhandler{
	override bool InputProcess(inputevent e){
		if(e.type == InputEvent.TYPE_MOUSE){
			if(players[consoleplayer].buttons & BT_ATTACK || players[consoleplayer].buttons & BT_ALTATTACK){
				SendNetworkEvent("DoSo_MouseEvent",e.mousex,e.mousey,consoleplayer);
				return true;
			}
		}
		return false;
	}
	
	override void NetworkProcess(consoleevent e){
		if(e.name=="DoSo_MouseEvent"){
			playerinfo pla = players[e.args[2]];
			if(pla.readyweapon && pla.readyweapon is "doso_weap"){
				let wep = doso_weap(pla.readyweapon);
				if(!wep.child)
					return;
				
				float x = e.args[0]*cvar.getcvar("doso_mousesens",pla).getfloat();
				float y = e.args[1]*cvar.getcvar("doso_mousesens",pla).getfloat();
				
				if(pla.buttons & BT_ATTACK && pla.buttons & BT_ALTATTACK){
					wep.rotatew(x,y);
				}else{
					if(pla.buttons & BT_ATTACK){
						wep.swingw(x,y);
					}
					if(pla.buttons & BT_ALTATTACK){
						wep.movew(x,y);
					}
				}
			}
		}
	}
}

//handler for preventing standard game weapon and ammo spawns
class doso_spawnhandler:eventhandler{
	override void WorldThingSpawned(worldevent e){
		if(doso_blockweapspawns && e.thing is "weapon" && !(e.thing is "doso_weap")){
			e.thing.destroy();
		}
		if(doso_blockammospawns && e.thing is "ammo"){
			e.thing.destroy();
		}
	}
}